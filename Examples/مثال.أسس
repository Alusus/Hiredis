اشمل "مـتم/طـرفية"؛
اشمل "مـتم/نـظام"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/Hiredis"، "هـايردس.أسس")؛

دالة ابدأ {
    استخدم مـتم؛

    عرف سياق: مؤشر[هـايردس.سـياق] = 0؛
    عرف رد: مؤشر[هـايردس.الـرد]؛

    سياق = هـايردس.اتصل("0.0.0.0", 6379)؛

    إذا سياق == 0 {
        طـرفية.اطبع("فشل الاتصال: فشل إنشاء السياق.\ج")؛
        نـظام.اخرج(1)؛
    } وإلا إذا سياق~محتوى.خطأ != 0 {
        طـرفية.اطبع("فشل الاتصال: %s\ج", سياق~محتوى.نص_الخطأ~مؤشر)؛
        نـظام.اخرج(1)؛
    }

    رد = هـايردس.أمر(سياق, "SET %s %s", "foo", "hello world")؛
    طـرفية.اطبع("SET: %s\n", رد~محتوى.نص)؛
    هـايردس.حرر(رد)؛

    رد = هـايردس.أمر(سياق, "PING")؛
    طـرفية.اطبع("PING: %s\n", رد~محتوى.نص)؛
    هـايردس.حرر(رد)؛

    رد = هـايردس.أمر(سياق, "SET %b %b", "bar", 3i64, "hello", 5i64)؛
    طـرفية.اطبع("SET (binary API): %s\n", رد~محتوى.نص)؛
    هـايردس.حرر(رد)؛

    رد = هـايردس.أمر(سياق, "GET foo")؛
    طـرفية.اطبع("GET foo: %s\n", رد~محتوى.نص)؛
    هـايردس.حرر(رد)؛

    رد = هـايردس.أمر(سياق, "INCR counter")؛
    طـرفية.اطبع("INCR counter: %lld\n", رد~محتوى.عدد_صحيح)؛
    هـايردس.حرر(رد)؛

    رد = هـايردس.أمر(سياق, "DEL mylist")؛
    هـايردس.حرر(رد)؛

    هـايردس.حرر(سياق)؛
}

ابدأ()؛

